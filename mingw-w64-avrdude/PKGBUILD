# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=avrdude
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.2
pkgrel=1
pkgdesc='Software for programming Atmel AVR Microcontrollers (mingw-w64)'
arch=('any')
url="http://www.nongnu.org/avrdude/"
license=('GPL')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "grep"
  "bison"
  "flex"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-libftdi"
  "${MINGW_PACKAGE_PREFIX}-libusb"
  "${MINGW_PACKAGE_PREFIX}-libelf"
)
options=('staticlibs' 'strip')
source=(
  "http://download.savannah.gnu.org/releases/avrdude/avrdude-${pkgver}.tar.gz"
  'all.patch'
)

sha256sums=(
  'e65f833493b7d63a4436c7056694a0f04ae5b437b72cc084e32c58bc543b0f91'
  'SKIP'
)

prepare()
{
  cd "${srcdir}/${_realname}-${pkgver}"

  # Delete binaries.
  rm windows/giveio.sys

  # Let's use mingw-w64's hidsdi.h instead of AVRDUDE's.
  rm my_ddk_hidsdi.h

  patch -p1 -i ../all.patch

  ./bootstrap
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "../${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}${MINGW_PREFIX}"

  # Remove loaddrv.exe because we aren't distributing any drivers one could use
  # with it and driver signing requirements in modern versions of Windows mean
  # it is unlikely to work (it only claims to support Windows NT/2000/XP).
  # Also, with a generic name like loaddrv, it could cause a name collision.
  rm bin/loaddrv.exe

  # As we can see from confwin.c, AVRDUDE searches for avrdude.conf
  # using the PATH; it does not find it in /etc.
  mv etc/avrdude.conf bin

  cd "${srcdir}/${_realname}-${pkgver}"
  install -D COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
