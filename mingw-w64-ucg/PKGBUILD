# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=ucg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.2
pkgrel=2
pkgdesc='Fast grep-like tool for searching large bodies of source code (mingw-w64)'
arch=('any')
url="https://gvansickle.github.io/ucg/"
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-pcre")
options=('staticlibs' 'strip')
_gnulib_ver=730c5249cc17a4ebb7afcb3f2f4e73664eeff5ce
source=("https://github.com/gvansickle/ucg/archive/${pkgver}.tar.gz"
        "gnulib.tar.gz::http://git.savannah.gnu.org/gitweb/?p=gnulib.git;a=snapshot;h=${_gnulib_ver};sf=tgz"
        "01-gnulib.patch"
        "02-uid.patch"
        "03-fts.patch"
        "04-malloc-stats.patch"
        "05-mmap.patch"
        "06-gnulib-fts-cycle.patch")

sha256sums=('597527d3e8902b8ab936f04a11c8b5d93c33e372638bd63d08c8013fec9a95d2'
            '0f5ab2134a42b233d2d7dbb5296a4c7a0fd6fda4a54381c58cbda1be2519ecbb'
            '10e50281e32f79a591dfa524fd9e24a8a4a06c8f4e8be51450e2854e29c1ac5b'
            'b867482b0e72fed4ebef8265c09203ec62c74c473aab386d5905f51b74b9f5de'
            '42640c270ad538064f4ee07947747188d7f9d9bcde1b7fa81b9ddb31bbb0613c'
            'caf2961d830f340bd69ea643d2691d0d63a96fac91b00e23360fcfba5578b3e5'
            '12d60d83699b0609601b63dc030cc5c3334d783c4b4ffdd37d36509e8a32eeb1'
            'bc4062601509ba45a93a498ae9e9ae37ce920f0084a9ad37c28f6b633420ff07')

prepare() {
  cd "${_realname}-${pkgver}"
  "../gnulib-${_gnulib_ver:0:7}/gnulib-tool" --import argp fts fstatat progname
  patch -p1 -i ../01-gnulib.patch
  patch -p1 -i ../02-uid.patch
  patch -p1 -i ../03-fts.patch
  patch -p1 -i ../04-malloc-stats.patch
  patch -p1 -i ../05-mmap.patch
  patch -p1 -i ../06-gnulib-fts-cycle.patch
  autoreconf -i
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  "../${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}"

  make
}

# TODO: make check

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/${_realname}-${pkgver}"
  install -D COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
