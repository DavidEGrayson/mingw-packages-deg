# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=ucg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.2
pkgrel=2
pkgdesc='Fast grep-like tool for searching large bodies of source code (mingw-w64)'
arch=('any')
url="https://gvansickle.github.io/ucg/"
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-pcre")
options=('staticlibs' 'strip')
_gnulib_ver=730c5249cc17a4ebb7afcb3f2f4e73664eeff5ce
source=("https://github.com/gvansickle/ucg/archive/${pkgver}.tar.gz"
        "gnulib.tar.gz::http://git.savannah.gnu.org/gitweb/?p=gnulib.git;a=snapshot;h=${_gnulib_ver};sf=tgz"
        "01-gnulib.patch"
        "02-uid.patch"
        "03-fts.patch")

sha256sums=('597527d3e8902b8ab936f04a11c8b5d93c33e372638bd63d08c8013fec9a95d2'
            '0f5ab2134a42b233d2d7dbb5296a4c7a0fd6fda4a54381c58cbda1be2519ecbb'
            '004d0fcaafa330c58f1902abac88099a50b05169aefe4d61f447aa5626a8a712'
            'b867482b0e72fed4ebef8265c09203ec62c74c473aab386d5905f51b74b9f5de'
            '42640c270ad538064f4ee07947747188d7f9d9bcde1b7fa81b9ddb31bbb0613c')

prepare() {
  cd "${_realname}-${pkgver}"
  "../gnulib-${_gnulib_ver:0:7}/gnulib-tool" --import argp fts
  patch -p1 -i ../01-gnulib.patch
  patch -p1 -i ../02-uid.patch
  patch -p1 -i ../03-fts.patch
  autoreconf -i
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  "../${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/${_realname}-${pkgver}"
  install -D COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
