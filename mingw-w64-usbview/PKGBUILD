# Maintainer: David Grayson <davidegrayson@gmail.com>

# An alternative copy of this program can be found at:
# https://github.com/Microsoft/Windows-driver-samples/tree/master/usb/usbview

_realname=usbview
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.20140402
pkgrel=1
pkgdesc='GUI for browsing all USB controllers and connected USB devices on your computer.'
arch=('any')
url='https://msdn.microsoft.com/en-us/library/windows/hardware/ff560019.aspx'
license=('custom')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-intsafe"
  'dos2unix'
  'patch'
)
depends=()
options=('strip')
source=(
  "https://code.msdn.microsoft.com/windowshardware/Windows-8-Driver-Samples-5e1aa62e/file/97069/26/Windows%20Driver%20Kit%20(WDK)%208.1%20Samples.zip"
)

sha256sums=(
  '49f005dfd72fba8d5f9727a9085826c593f282346e3750f10d122da52c0e31f2'
)

prepare() {
  cd "${srcdir}/USBView sample application/C++"
  s=../../..

  # Convert all the line endings to Unix right now because the patch
  # command will end up doing it automatically for any file it
  # patches, which will make it hard to use "diff -u -r" to prepare
  # more patches.
  dos2unix *.c *.h *.hpp *.rc

  # strsafe.h must be after tchar.h in mingw-w64
  patch -p1 -i "${s}/01-strsafe-after-tchar.patch"

  # mingw-w64's sal.h doesn't include enough annotations:
  # https://msdn.microsoft.com/en-us/library/hh916382.aspx
  # https://msdn.microsoft.com/en-us/library/jj159529.aspx
  cp "${s}/my_sal.h" .
  patch -p1 -i "${s}/02-my-sal.patch"

  # We exclude the XML stuff because it requires Visual C++ (.NET).
  rm usbschema.hpp xmlhelper.cpp
  cp "${s}/my_xmlhelper.c" .

  # Fix some syntax errors that windres complains about
  patch -p1 -i "${s}/03-resource-fix.patch"
}

build() {
  cd "${srcdir}/USBView sample application"
  mkdir -p "build-${MINGW_CHOST}"
  cd "build-${MINGW_CHOST}"

  windres ../C++/uvcview.rc rc.so

  # Optimization level of at least O1 is required to make GCC actually
  # inline the functions in uvcdesc.h, so they don't require a separate
  # definition somewhere.  Removing the -O1 option would cause a linker
  # error.  This is kind of sketchy, but was the easiest way to compile.

  gcc --std=c99 -O1 \
    -DNTDDI_VERSION=0x06020000 -D_WIN32_WINNT=0x0602 \
    -DSTRSAFE_NO_DEPRECATE \
    ../C++/*.c rc.so \
    -lcomctl32 -lcomdlg32 -lsetupapi -lshell32 -lshlwapi -lole32 -lgdi32 \
    -o usbview.exe
}

check() {
  echo hi
}

package() {
  cd "${srcdir}/USBView sample application/"
  install -Dm644 license.rtf "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.rtf"
  cd "build-${MINGW_CHOST}"
  mkdir "${pkgdir}${MINGW_PREFIX}/bin/"
  install -Dm755 usbview.exe "${pkgdir}${MINGW_PREFIX}/bin/"
}
