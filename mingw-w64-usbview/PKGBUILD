# Maintainer: David Grayson <davidegrayson@gmail.com>

# An alternative copy of this program can be found at:
# https://github.com/Microsoft/Windows-driver-samples/tree/master/usb/usbview

_realname=usbview
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.20140402
pkgrel=2
pkgdesc='GUI for browsing all USB controllers and connected USB devices on your computer.'
arch=('any')
url='https://code.msdn.microsoft.com/windowshardware/USBView-sample-application-e3241039'
license=('custom')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  'dos2unix'
  'patch'
)
depends=()
options=('strip')
source=(
  "https://code.msdn.microsoft.com/windowshardware/USBView-sample-application-e3241039/file/51333/38/USBView%20sample%20application.zip"
)

sha256sums=(
  'fd07e8b20f9cff7efd8c01bf2b9cc9ac39ba85c76e56287b80d8abf38ec92287'
)

prepare() {
  cd "${srcdir}/C++"
  s=../..

  # Convert all the line endings to Unix right now because the patch
  # command will end up doing it automatically for any file it
  # patches, which will make it hard to use "diff -u -r" to prepare
  # more patches.
  dos2unix *.c *.h *.hpp *.rc

  # strsafe.h must be after tchar.h in mingw-w64
  patch -p1 -i "${s}/01-strsafe-after-tchar.patch"

  # mingw-w64's sal.h doesn't include enough annotations:
  # https://msdn.microsoft.com/en-us/library/hh916382.aspx
  # https://msdn.microsoft.com/en-us/library/jj159529.aspx
  cp "${s}/my_sal.h" .
  patch -p1 -i "${s}/02-my-sal.patch"

  # We exclude the XML stuff because it requires Visual C++ (.NET).
  rm usbschema.hpp xmlhelper.cpp
  cp "${s}/my_xmlhelper.c" .

  # Fix some syntax errors that windres complains about
  patch -p1 -i "${s}/03-resource-fix.patch"
}

build() {
  cd "${srcdir}"
  mkdir -p "build-${MINGW_CHOST}"
  cd "build-${MINGW_CHOST}"

  windres ../C++/uvcview.rc rc.so

  # Optimization level of at least O1 is required to make GCC actually
  # inline the functions in uvcdesc.h, so they don't require a separate
  # definition somewhere.  Removing the -O1 option would cause a linker
  # error.  This is kind of sketchy, but was the easiest way to compile.

  gcc -mwindows --std=c99 -O1 \
    -DNTDDI_VERSION=0x06020000 -D_WIN32_WINNT=0x0602 \
    -DSTRSAFE_NO_DEPRECATE \
    ../C++/*.c rc.so \
    -lcomctl32 -lcomdlg32 -lsetupapi -lshell32 -lshlwapi -lole32 -lgdi32 \
    -o usbview.exe
}

package() {
  cd "${srcdir}"
  install -Dm644 license.rtf "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.rtf"
  cd "build-${MINGW_CHOST}"
  mkdir "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm755 usbview.exe "${pkgdir}${MINGW_PREFIX}/bin"
}
