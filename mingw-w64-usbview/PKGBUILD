# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=usbview
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides="${MINGW_PACKAGE_PREFIX}-${_realname}"
conflicts="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.0
pkgrel=1
pkgdesc='GUI for browsing all USB controllers and connected USB devices on your computer (mingw-w64)'
arch=('any')
url=''
license=('custom')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  'patch'
)
depends=()
options=('strip')
source=(
  "wds::git+https://github.com/Microsoft/Windows-driver-samples"
  'my_xmlhelper.c'
  '01-uvcview-includes.patch'
  '03-resource-fix.patch'
  '04-redefinition.patch'
  '05-connection-statuses.patch'
)

sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)

#pkgver() {
#  cd "${srcdir}/wds"
#  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
#}

prepare() {
  cd "${srcdir}/wds/usb/usbview"

  # dos2unix -q *.c *.h *.rc

  # strsafe.h must be after tchar.h in mingw-w64.
  # sal.h and usbspec.h are  needed.
  patch -p1 -i "${srcdir}/01-uvcview-includes.patch"

  # We exclude the XML stuff because it requires Visual C++ (.NET).
  rm usbschema.hpp xmlhelper.cpp
  cp "${srcdir}/my_xmlhelper.c" .

  # Fix some syntax errors that windres complains about
  patch -p1 -i "${srcdir}/03-resource-fix.patch"

  # Fix warnings about macro redefinitions.
  patch -p1 -i "${srcdir}/04-redefinition.patch"

  # Fix bad definition of array without a length.
  patch -p1 -i "${srcdir}/05-connection-statuses.patch"
}

build() {
  cd "${srcdir}"
  mkdir -p "build-${MINGW_CHOST}/include"
  cd "build-${MINGW_CHOST}"

  # Make sure mingw-w64's usb.h takes precedence over libusb-compat's.
  cp ${MINGW_PREFIX}/${MINGW_CHOST}/include/usb.h include

  windres ../wds/usb/usbview/uvcview.rc rc.so

  # The include hack below requires you to edit usb200.h to
  # include usbspec.h using angle brackets instead of quotes.
  # TODO: remove the hack after mingw-w64 gets patched

  gcc -mwindows --std=c99 ${CFLAGS} ${LDFLAGS} \
    -I/d/builds/mingw-packages-deg/include/ \
    -Iinclude \
    -DNTDDI_VERSION=0x06020000 -D_WIN32_WINNT=0x0602 \
    -DSTRSAFE_NO_DEPRECATE \
    ../wds/usb/usbview/*.c rc.so \
    -lcomctl32 -lcomdlg32 -lsetupapi -lshell32 -lshlwapi -lole32 -lgdi32 \
    -o usbview.exe
}

package() {
  cd "${srcdir}"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.rtf"
  cd "build-${MINGW_CHOST}"
  mkdir "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm755 usbview.exe "${pkgdir}${MINGW_PREFIX}/bin"
}
