# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=confuse
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.7.r247
pkgrel=1
pkgdesc='Library for parsing configuration files (mingw-w64)'
arch=('any')
url="https://www.intra2net.com/en/developer/libftdi/"
license=('custom')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-gettext"
)
depends=()  # TODO: run-time dependencies for gettext (libintl.dll)?
options=('staticlibs' 'strip')
source=(
  "https://github.com/DavidEGrayson/libconfuse/archive/david.zip"
)

sha256sums=('f28a9c5ee2af32af9042e99f256a93b998aed841311e1be2b21c75fd3c8f303d')

prepare() {
  # We can't use autogen.sh because it runs configure.

  cd "${srcdir}/lib${_realname}-david"
  autopoint
  aclocal -I m4
  autoconf
  autoheader
  libtoolize --automake
  automake --add-missing
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "../lib${_realname}-david/configure" \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared \
    --disable-examples \

  # TODO: man pages

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/lib${_realname}-david"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
